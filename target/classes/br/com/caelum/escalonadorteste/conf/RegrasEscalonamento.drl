import org.optaplanner.core.api.score.buildin.hardsoft.HardSoftScoreHolder;

import java.time.LocalDate;

import br.com.caelum.escalonadorteste.modelo.ParametrosDeEscalonamento
import br.com.caelum.escalonadorteste.modelo.Turma
import br.com.caelum.escalonadorteste.modelo.Instrutor
import br.com.caelum.escalonadorteste.modelo.Curso

import java.util.ArrayList;

global HardSoftScoreHolder scoreHolder;

rule "não sabe dar o curso"
    when
    	Turma(instrutor != null, !instrutor.sabeDarOCurso(this))
    then
        scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "não consegue dar aula no período"
    when
    	Turma(instrutor != null, !instrutor.consegueDarAulaNoPeriodo(this))
    then
        scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "não está disponível nos dias da turma"
	when
		Turma(instrutor != null, !instrutor.estaDisponivelNosDiasDaTurma(this))
	then
		scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "mais de X horas de aula"
	when
		$parametros : ParametrosDeEscalonamento()
		$instrutor : Instrutor()
		Number(intValue > $parametros.numeroMaximoDeHorasPorInstrutor) from 
			accumulate($turma : Turma(instrutor == $instrutor) and $curso : Curso(codigo == $turma.codigoCurso); sum($curso.getCargaHoraria())) 
	then
		scoreHolder.addSoftConstraintMatch(kcontext, -10);
end

rule "data de um curso conflita com outro"
	when
		$outraTurma : Turma($instrutor : instrutor != null)
		Turma($outraTurma != this, instrutor == $instrutor, aulasConflitamCom($outraTurma))
	then
		scoreHolder.addHardConstraintMatch(kcontext, -10);
end

rule "mais de X turmas seguidas"
	when
		$parametros : ParametrosDeEscalonamento()
		$instrutor : Instrutor()
		Number(intValue > $parametros.numeroMaximoDeTurmasSeguidas) from
			accumulate($turma : Turma(instrutor == $instrutor); maximoDeTurmasSeguidas($turma))
	then
		scoreHolder.addSoftConstraintMatch(kcontext, -10);	
end

rule "instrutor planejado foi trocado"
	when
		Turma(instrutorPlanejado != null, instrutorPlanejado != instrutor)
	then
		scoreHolder.addSoftConstraintMatch(kcontext, -1);
end

rule "instrutor não está disponível para viagem"
	when
		Turma(instrutor != null, ehViagem, !instrutor.estaDisponivelParaViagem())
	then
		scoreHolder.addHardConstraintMatch(kcontext, -10); 		
end
